# -*- mode: python; -*-

#
# Copyright (c) 2013 Juniper Networks, Inc. All rights reserved.
#

import sys

Import('BuildEnv')
env = BuildEnv.Clone()

sources = [
    'MANIFEST.in',
    'CHANGES.txt',
    'LICENSE.txt',
    'README.txt',
    'contrail-topology.conf',
    'requirements.txt',
    'setup.py',
    'contrail_topology/__init__.py',
    'contrail_topology/main.py',
    'contrail_topology/analytic_client.py',
    'contrail_topology/controller.py',
    'contrail_topology/config.py',
    'contrail_topology/topology_uve.py',
    'contrail_topology/topology_config_db.py',
    'contrail_topology/topology_config_handler.py'
    ]

common_sources = [
    '#src/contrail-analytics/contrail-opserver/analytics_logger.py',
    '#src/contrail-analytics/contrail-opserver/config_handler.py'
]

src_sandesh = env.SandeshGenPy('link.sandesh', 'contrail_topology/sandesh/', False)
topology_info_pkg = env.SandeshGenPy('topology_info.sandesh', 'contrail_topology/sandesh/', False)
cpuinfo_pkg = env.SandeshGenPy('#src/contrail-common/base/sandesh/cpuinfo.sandesh', 'contrail_topology/sandesh/nodeinfo/', False)
process_info_pkg = env.SandeshGenPy('#src/contrail-common/base/sandesh/process_info.sandesh', 'contrail_topology/sandesh/nodeinfo/', False)
nodeinfo_pkg = env.SandeshGenPy('#src/contrail-common/base/sandesh/nodeinfo.sandesh', 'contrail_topology/sandesh/', False)
derived_stats_pkg = env.SandeshGenPy('#src/contrail-common/sandesh/library/common/derived_stats_results.sandesh', 'contrail_topology/sandesh/analytics/', False)

common_sources_rules = [env.Install('contrail_topology', f) for f in common_sources]

version = '0.1.0'               # Make this match what is in setup.py

# XXX Was this disabled/removed as part of repo split? If so, it might
# explain the version discrepancy. Probably this should be done via
# version setting in setup.py
#with open("CHANGES.txt") as f:
#    version = f.read().split(",")[0][1:]

sdist_depends = [ nodeinfo_pkg, cpuinfo_pkg, process_info_pkg,
                  derived_stats_pkg, topology_info_pkg, common_sources_rules ]
cd_cmd = 'cd ' + Dir('.').path + ' && '
sdist_gen = env.Command('dist/contrail_topology-' \
        + version + '.tar.gz',
        sources + src_sandesh, cd_cmd + 'python setup.py sdist')
env.Depends(sdist_gen, sdist_depends)
env.Alias('src/contrail-analytics/contrail-topology/' + \
        'contrail_topology:sdist', sdist_gen)


src_rules = [env.Install(Dir("."),
    '#src/contrail-analytics/contrail-topology/' + f) for f in sources]
src_rules += [env.Install(Dir("."), f) for f in common_sources]
src_rules.append(env.Install(Dir("."),
        '#src/contrail-analytics/contrail-opserver/run_tests.sh'))

test_sources = [
    'requirements.txt',
    'test-requirements.txt',
    '.testr.conf',
    '.coveragerc',
    'test',
]

src_rules += [env.Install(Dir("."),
    '#src/contrail-analytics/contrail-topology/' + f) for f in test_sources]

import os

buildspace_link = os.environ.get('CONTRAIL_REPO')
if buildspace_link:
    # in CI environment shebang limit exceeds for python
    # in easy_install/pip, reach to it via symlink
    top_path = buildspace_link + '/' + Dir('.').path
    top_dir = Dir(top_path)
else:
    top_dir = Dir('.')

testr_str = 'bash -c "set -o pipefail && cd ' + Dir(top_dir).path + \
            ' && tools_path=../config/common/tests/' + \
            ' omit_list="contrail_topology/gen_py"' \
            ' CFLAGS= LD_LIBRARY_PATH=${LD_LIBRARY_PATH}:../../../build/lib' \
            ' python setup.py run_tests %s 2>&1 |' \
            ' tee %s.log"'

env.Append(ENV = {'BUILDTOP': Dir(env['TOP']).abspath,
                    'COVERAGERUN': str(env['OPT'] == 'coverage')})

test_depends = [
        '/config/common/dist/contrail-config-common-0.1dev.tar.gz',
        '/sandesh/common/dist/sandesh-common-0.1dev.tar.gz',
        '/tools/sandesh/library/python/dist/sandesh-0.1dev.tar.gz',
        '/opserver/dist/opserver-0.1dev.tar.gz',
]

if env['OPT'] == 'coverage':
    test_cmd = env.Command('coveragetest.log', sdist_gen,
            testr_str % ('--coverage', 'coveragetest'))
elif env['PYTESTARG']:
    test_cmd = env.Command(env['PYTESTARG'] + '.log', sdist_gen,
            testr_str % ('--testrun="%s"' % (env['PYTESTARG']),
            env['PYTESTARG']))
else:
    test_cmd = env.Command('test.log', sdist_gen,
                           testr_str % ('', 'test'))

# XXX Need to extend SetupPyTestSuite*() to handle above variations
#
if 'BUILD_ONLY' in env['ENV']:
    test_cmd = env.Command('build_only.log', sdist_gen, 'touch $TARGET')

env.Depends(test_cmd, [env['TOP']+x for x in test_depends])
env.Depends(test_cmd, src_rules)

env.Alias('src/contrail-analytics/contrail-topology:test', test_cmd)
env.AlwaysBuild(test_cmd)

doc_files = []
#doc_files += env['BASE_DOC_FILES']
#doc_files += env['SANDESH_DOC_FILES']
doc_files += env['ANALYTICS_DOC_FILES']
doc_files += env.SandeshGenDoc('link.sandesh')
doc_files += env.SandeshGenDoc('topology_info.sandesh')

if 'install' in BUILD_TARGETS:
    install_cmd = env.Command(None, sources + src_sandesh,
                              cd_cmd + 'python setup.py install %s' %
                              env['PYTHON_INSTALL_OPT'])
    env.Depends(install_cmd, sdist_depends)
    env.Alias('install', install_cmd)
env.Alias('install', env.Install(env['INSTALL_CONF'],
            'contrail-topology.conf'))
env.Alias('install', env.Install(
    env['INSTALL_MESSAGE_DOC'] + '/contrail-topology/', doc_files))

# Local Variables:
# mode: python
# End:
